<?xml version='1.0' encoding='UTF-8'?><!-- -*- indent-tabs-mode: nil -*- --> 

<!-- Local Overrides for the gnome-doc-utils XSL stylesheets.
     
     Copyright (C) 2009, Collabora Ltd.

     Authors: Davyd Madeley <davyd.madeley@collabora.co.uk>
-->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:exsl="http://exslt.org/common"
                xmlns:set="http://exslt.org/sets"
                xmlns="http://www.w3.org/1999/xhtml"
                xmlns:str="http://exslt.org/strings"
                extension-element-prefixes="exsl"
                xsl:exclude-result-prefixes="set str"
                version="1.0"> 

  <xsl:import href="@PARENT_STYLESHEET@"/>

  <!-- this template matches the hack tag <embedhtml> and copies its
       contents verbatim into the output document.
       This allows us to embed XHTML directly into the output, e.g. from
       a syntax highlighter -->
  <xsl:template match="embedhtml">
   <xsl:copy-of select="./*">
    <xsl:call-template name="db2html.xref"/>
   </xsl:copy-of>
  </xsl:template>

  <!-- the header to put at the top of every page -->
  <xsl:template name="tp.page_header">
   <div id="header" style="margin-bottom:1ex;">
    <h1>Telepathy Developer's Manual</h1>
   </div>
   <div style="clear:both;"></div>
  </xsl:template>

  <!-- from g-d-u: modified to call out to template 'tp.page_header' -->
  <xsl:template name="db2html.division.top">
    <xsl:param name="node"/>
    <xsl:param name="info" select="/false"/>
    <xsl:param name="template"/>
    <xsl:param name="depth_of_chunk">
      <xsl:call-template name="db.chunk.depth-of-chunk">
        <xsl:with-param name="node" select="$node"/>
      </xsl:call-template>
    </xsl:param>
    <xsl:param name="prev_id">
      <xsl:choose>
        <xsl:when test="$depth_of_chunk = 0">
          <xsl:if test="$info and $db.chunk.info_chunk">
            <xsl:value-of select="$db.chunk.info_basename"/>
          </xsl:if>
        </xsl:when>
        <xsl:otherwise>
          <xsl:call-template name="db.chunk.chunk-id.axis">
            <xsl:with-param name="node" select="$node"/>
            <xsl:with-param name="axis" select="'previous'"/>
            <xsl:with-param name="depth_in_chunk" select="0"/>
            <xsl:with-param name="depth_of_chunk" select="$depth_of_chunk"/>
          </xsl:call-template>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:param>
    <xsl:param name="next_id">
      <xsl:call-template name="db.chunk.chunk-id.axis">
        <xsl:with-param name="node" select="$node"/>
        <xsl:with-param name="axis" select="'next'"/>
        <xsl:with-param name="depth_in_chunk" select="0"/>
        <xsl:with-param name="depth_of_chunk" select="$depth_of_chunk"/>
      </xsl:call-template>
    </xsl:param>
    <xsl:param name="prev_node" select="key('idkey', $prev_id)"/>
    <xsl:param name="next_node" select="key('idkey', $next_id)"/>
    
    <xsl:call-template name="tp.page_header"/>

    <xsl:if test="$db2html.navbar.top">
      <xsl:call-template name="db2html.navbar">
        <xsl:with-param name="node" select="$node"/>
        <xsl:with-param name="prev_id" select="$prev_id"/>
        <xsl:with-param name="next_id" select="$next_id"/>
        <xsl:with-param name="prev_node" select="$prev_node"/>
        <xsl:with-param name="next_node" select="$next_node"/>
        <xsl:with-param name="position" select="'top'"/>
      </xsl:call-template>
    </xsl:if>
  </xsl:template>

  <!-- override this template to add a Telepathy logo -->
  <xsl:template name="db2html.sidenav">
    <xsl:param name="node" select="."/>
    <xsl:param name="template"/>
    <div class="sidenav">
      <center><img src="figures/telepathy-logo.png" alt="Telepathy Logo"/></center>
      <xsl:call-template name="db2html.autotoc">
        <xsl:with-param name="node" select="/"/>
        <xsl:with-param name="show_info" select="$db.chunk.info_chunk"/>
        <xsl:with-param name="is_info" select="$template = 'info'"/>
        <xsl:with-param name="selected" select="$node"/>
        <xsl:with-param name="divisions" select="/*"/>
        <xsl:with-param name="toc_depth" select="$db.chunk.max_depth + 1"/>
        <xsl:with-param name="labels" select="false()"/>
        <xsl:with-param name="titleabbrev" select="true()"/>
      </xsl:call-template>
    </div>
  </xsl:template>

</xsl:stylesheet>

